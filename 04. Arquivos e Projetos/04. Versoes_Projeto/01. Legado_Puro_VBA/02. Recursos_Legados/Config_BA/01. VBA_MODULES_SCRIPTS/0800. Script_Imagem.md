```vba
Attribute VB_Name = "Imagem"
Option Explicit

Private Const FILE_ROW = 1
Private Const HEADER_ROW = 3
Private Const START_ROW = 4
Private Const ALT_COL = 4
Private Const PATH_COL = 5

Sub PopulateXL(Optional byDummy As Byte)
    Dim objword As Word.Application
    Dim objDoc As Word.Document
    Dim objWS As Worksheet
    Dim Path As String
    
    Path = ActiveWorkbook.Path & "\"
    
    Set objword = New Word.Application
    If Planilha2.Range("E2").Value = "" Then
    Set objDoc = selectWordFile(objword, Path)
    Else
    Set objDoc = objword.Documents.Open(Planilha2.Range("E2").Value, , True, , , , , , , , , False)
    End If
    If objDoc Is Nothing Then GoTo CloseWord
    
    Planilha2.Range(("D4:G" & Planilha2.Range("D" & Rows.Count).End(xlUp).Row)).ClearContents
    
    Set objWS = Planilha2
    objWS.Cells(FILE_ROW, ALT_COL).Value = "Local do Arquivo"
    objWS.Cells(FILE_ROW, PATH_COL).Value = objDoc.FullName
    objWS.Cells(HEADER_ROW, ALT_COL).Value = "Texto Alternativo"
    objWS.Cells(HEADER_ROW, PATH_COL).Value = "Local da Figura"
    
    getTablesTitle objDoc, objWS, objword
    
    objDoc.Close (False)
    
    'new UserForm
    Substitute.Show
    
    'Unload Imagens
    'Planilha2.Visible = True
    'Planilha2.Activate
    'Planilha2.Cells(4, 2).Select
    
    
CloseWord:
    objword.Quit
    
End Sub

Sub substituteFigures(Optional byDummy As Byte)
Dim objWB As Excel.Workbook
Dim objWS As Excel.Worksheet
Dim objword As Word.Application
Dim objDoc As Word.Document
Dim Row As Long
Dim altText As String
Dim docPath As String
Dim picPath As String
           
    Set objWS = Planilha2
        
    Row = START_ROW
    altText = objWS.Cells(Row, ALT_COL)
    picPath = objWS.Cells(Row, PATH_COL)
    docPath = objWS.Cells(FILE_ROW, PATH_COL)
    
    If Dir(docPath) = "" Then ' FILE NOT FOUND
        MsgBox "Word File not found"
        Exit Sub
    End If
    
    Set objword = New Word.Application
    Set objDoc = objword.Documents.Open(docPath, , True, , , , , , , , , False)
    
    While altText <> ""
        Dim pic As InlineShape
        Set pic = Nothing
        
        If picPath <> "" And Dir(picPath) <> "" Then ' Check if file exists
            Set pic = findTableByTitle(objDoc, altText)
            If Not pic Is Nothing Then
                Call substituteFigure(pic, picPath)
            End If
        Else
            Set pic = findTableByTitle(objDoc, altText)
            If Not pic Is Nothing Then
                pic.AlternativeText = ""
            End If
        End If
        
        ' Update loop variables
        Row = Row + 1
        altText = objWS.Cells(Row, ALT_COL)
        picPath = objWS.Cells(Row, PATH_COL)
    Wend
    
    'Unload Imagens
    'Planilha1.Activate
    'Planilha2.Visible = False
    
    objword.Visible = True
    
    
End Sub


Private Sub substituteFigure(ByRef pic As InlineShape, ByRef newPath As String)
Dim objDoc As Word.Document
Dim newPic As Word.InlineShape
Dim h As Long
Dim w As Long

    ' Get picture attributes
    With pic
        h = .Height
        w = .Width
        Set objDoc = .Parent
        .Select
        .Delete
    End With
    
    Call objDoc.InlineShapes.AddPicture(newPath) ' The object returned by the AddPicture method, is not the picture created
    objDoc.Application.Selection.MoveRight wdCharacter, 1, wdExtend ' Using the previous selection to find the new picture
    Set newPic = objDoc.Application.Selection.InlineShapes(1)       ' Sets new picture through the selection
    
    ' Set old picture size
    With newPic
        .Height = h
        .Width = w
    End With

End Sub

Private Function selectWordFile(objword As Word.Application, Optional ByVal Path As String) As Word.Document
Dim dlgSelectFile As FileDialog
Dim objDoc As Word.Document

Set dlgSelectFile = Application.FileDialog(msoFileDialogFilePicker)

    With dlgSelectFile
        .AllowMultiSelect = False
        .Title = "Select Word File"
        .InitialFileName = Path
        .Filters.Clear
        .Filters.Add "Word Files", "*.docx, *.doc, *.docm"
        .FilterIndex = 1
    End With
    
    If Not dlgSelectFile.Show Then
        objword.Quit
        Set objDoc = Nothing
        Exit Function
    End If

    Set objDoc = objword.Documents.Open(dlgSelectFile.SelectedItems(1), , True, , , , , , , , , False)
    
    Set selectWordFile = objDoc
End Function

Private Sub getTablesTitle(ByRef objDoc As Word.Document, ByRef objWS As Worksheet, ByRef objword As Word.Application)

Dim Row As Long
Dim shape As Word.InlineShape
Dim cht As ChartObject
Dim str As String
Dim srn As Word.Range

Application.ScreenUpdating = False

    Dim ndoc As Word.Document
    Set ndoc = objword.Documents.Add

    Row = START_ROW
    'loop through inline shapes
    For Each shape In objDoc.InlineShapes
        'check if the current shape is a valid inlineShape
        If isValid(shape) Then
            shape.Select   ' Selection is needed, otherwise raises error "80004005 - AlternativeMethod has failed" on some files
            If shape.AlternativeText <> "" Then
                objWS.Cells(Row, 4).Value = shape.AlternativeText
                
                'To export and show in the userform
                If Planilha2.Range("G3").Value = "Exemplos" Then
                
                If shape.Height > 400 Then
                    Planilha2.Range("G" & Row).Value = "sim"
                    Set cht = Planilha2.ChartObjects.Add(Left:=Planilha2.Range("A20").Left, _
                    Width:=shape.Height, top:=Planilha2.Range("A20").top, Height:=shape.Width)
                Else
                    Set cht = Planilha2.ChartObjects.Add(Left:=Planilha2.Range("A20").Left, _
                    Width:=shape.Width, top:=Planilha2.Range("A20").top, Height:=shape.Height)
                End If
                
                cht.ShapeRange.Fill.Visible = msoFalse
                cht.ShapeRange.Line.Visible = msoFalse

                str = "V:\Abteilungen\SaoPaulo\PQK-M\Extern\Config_BA\Substitute\"
                
                Set srn = shape.Range
                srn.Expand wdSentence
                srn.Copy
                
                With ndoc
                
                .Activate
                
                .Range.Paste
               
                If .InlineShapes(1).Height > 400 Then
                    objword.Visible = True
                    'Stop
                    .InlineShapes(1).ConvertToShape
                    .Shapes(1).IncrementRotation 90
                    .Shapes(1).ConvertToInlineShape
                End If
                    .InlineShapes(1).Range.Copy 'AsPicture
                    objword.Visible = False
                End With
                
                Application.ScreenUpdating = True
                cht.Activate
                cht.Chart.Paste
                cht.Chart.Export Filename:=str & Row & ".bmp"
                cht.Delete
                Application.ScreenUpdating = False
                
                End If
                
                Row = Row + 1
                
            End If
        End If
    Next
    If Planilha2.Range("D4").Value = "" Then
        MsgBox "O arquivo no apresenta imagens marcadas para substituir.", vbOKOnly + vbInformation, "Substitio no necessria!"
        End
    End If

    ndoc.Close False

Application.ScreenUpdating = True
    
End Sub

Private Function findTableByTitle(objDoc As Word.Document, altText As String) As Word.InlineShape
Dim shape As Word.InlineShape
    
    Set findTableByTitle = Nothing
    
    For Each shape In objDoc.InlineShapes
        'check if the current shape is a valid inlineShape
        If isValid(shape) Then
            shape.Select
            If shape.AlternativeText = altText Then
                Set findTableByTitle = shape
                Exit Function
            End If
        End If
    Next
    
End Function

Private Function isValid(ByRef shape As InlineShape) As Boolean
    isValid = False
    Select Case (shape.Type)   ' This selection is needed, otherwise raises error "80004005 - AlternativeMethod has failed" on some files
        Case wdInlineShapePicture, wdInlineShapeEmbeddedOLEObject, wdInlineShapeLinkedOLEObject, wdInlineShapeLinkedPicture:
            isValid = True
    End Select
End Function
```